plugins {
    id 'com.android.library' version '8.0.2'
    id 'org.jetbrains.kotlin.android' version '1.6.20'
    id 'maven-publish'
}

android {
    namespace 'com.digidemic.cheers'
    compileSdk 33

    defaultConfig {
        minSdk 4
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
            "-Xopt-in=kotlin.RequiresOptIn"
        ]
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    testImplementation 'junit:junit:4.13.2'
}

afterEvaluate {
    android.libraryVariants.each {variant ->
        publishing.publications.create(variant.name, MavenPublication){
            from components.findByName(variant.name)

            groupId = 'com.github.cheers'
            artifactId = "cheers"
            version = '1.1.0'

            pom {
                name = 'Cheers'
                description = 'An Android Toast wrapper to queue and show multiple messages one at a time in sequential order.'
                url = 'https://github.com/Digidemic/cheers'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'Digidemic'
                        name = 'Adam Steinberg'
                        email = 'digidemic@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git://github.com/Digidemic/cheers.git'
                    developerConnection = 'scm:git://github.com/Digidemic/cheers.git'
                    url = 'git://github.com/Digidemic/cheers.git'
                }
            }
        }
    }
}